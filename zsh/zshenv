[[ ! -z $ZPROFILE ]] && zmodload zsh/zprof && zprof
setopt no_global_rcs
source_if_exist () {
  [[ -f "$1" ]] || return
  zcompile_if_newer "$1"
  source "$1"
}
zcompile_if_newer () { [[ ! -f "$1.zwc" || "${1:A}" -nt "$1.zwc" ]] && zcompile "$1" }

source_if_exist $HOME/.zsh/zshenv.functions

# brew
if is_linux ; then
  homebrew_dir=/home/linuxbrew/.linuxbrew
elif is_mac_apple ; then
  homebrew_dir=/opt/homebrew
elif is_mac_intel ; then
  homebrew_dir=/usr/local
fi
if whence -p $homebrew_dir/bin/brew >/dev/null ; then
  eval "$($homebrew_dir/bin/brew shellenv)"
  path=($homebrew_dir/bin(N-/) $homebrew_dir/sbin(N-/) $path)
  # tuning
  # $homebrew_dir/opt/coreutils = $(brew --prefix coreutils)
  path=($homebrew_dir/opt/coreutils/libexec/gnubin(N-/) $path)
fi

source_if_exist $HOME/.zsh/zshenv.xxenv

export LANG=ja_JP.UTF-8
# manpath（非インタラクティブでは不要）
if [[ -o interactive ]]; then
  MANPATH=
  export MANPATH=`manpath`
fi
export GISTY_DIR=$HOME/dev/gists
export DOCKER_BUILDKIT=1

# git-tasukete
path=($HOME/.zsh/git-tasukete.git(N-/) $HOME/android-sdk(N-/) $path)

# php composer
path=($HOME/.composer/vendor/bin(N-/) $path)

# go
export GOPATH=$HOME/go
path=($HOME/go/bin(N-/) $path)

path=($HOME/bin/*(N-/) $HOME/bin(N-/) $path)

# awsenv（補完はインタラクティブ専用）
if [[ -o interactive ]] && whence -p awsenv >/dev/null ; then
  . <(awsenv completions)
  setaws () {
      case $1 in
          unset|set)
              eval "$(awsenv $@)"
              ;;
          *)
              awsenv $@
              ;;
      esac
  }
fi

# mkrenv
if whence -p mkrenv >/dev/null ; then
  setmkr () {
    [[ $# -gt 0 ]] && eval "$(mkrenv $@)"
  }
fi

typeset -U path
